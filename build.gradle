buildscript {
	repositories {
		jcenter()
		maven {
			name = 'forge'
			url = 'http://files.minecraftforge.net/maven'
		}
		maven {
			name = 'SpongePowered'
			url = 'https://repo.spongepowered.org/repository/maven-public/'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version "1.0"
group "c.uriah" // http://maven.apache.org/guides/mini/guide-naming-conventions.html

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
compileJava {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

minecraft {
	version = "1.12.2-14.23.5.2768"
	runDir = 'run'
	mappings = mappings = "snapshot_20171003"
	coreMod = 'c.uriah.radar.mixin.MixinLoader'
	makeObfSourceJar = false
}

repositories {
	jcenter()
    mavenCentral()

	maven {
		name = 'spongepowered-repo'
		url = 'https://repo.spongepowered.org/repository/maven-public/'
	}
}

dependencies {
	compile("org.spongepowered:mixin:0.7.4-SNAPSHOT") {
		exclude module: 'launchwrapper'
		exclude module: 'guava'
		exclude module: 'gson'
		exclude module: 'commons-io'
	}
}

processResources {
	inputs.property 'version', project.version
	inputs.property 'mcversion', project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
	dependencies {
		// mixin
		include(dependency('org.spongepowered:mixin'))
	}
	exclude 'dummyThing'
	exclude 'LICENSE.txt'
	classifier = 'release'
}

mixin {
	defaultObfuscationEnv searge
	add sourceSets.main, 'mixins.radar.refmap.json'
}

reobf {
	shadowJar {
		mappingType = 'SEARGE'
		classpath = sourceSets.main.compileClasspath
	}
}

jar {
	manifest {
		attributes(
				'MixinConfigs': 'mixins.radar.json',
				'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
				'TweakOrder': 0,
				'FMLCorePluginContainsFMLMod': 'true',
				'FMLCorePlugin': 'c.uriah.radar.mixin.MixinLoader',
				'ForceLoadAsMod': 'true'
		)
	}
}

build.dependsOn(shadowJar)
